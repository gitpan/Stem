# Copyright 2000 by Stem Systems, Inc. All rights reserved.

# If you have this software as part of the prototype release, you are
# not allowed to distribute any copies to anyone. This software is not
# to shown to anyone else without prior permission from Stem Systems.

use strict ;

package Stem::TTY::Sock ;

use Carp ;
use Stem::AsyncIO ;

my $attr_spec = [
	{
		'name'		=> 'port',
		'default'	=> 10_000,
		'command_arg'	=> 'tty_port',
		'help'		=> <<HELP,
HELP
	},

	{
		'name'		=> 'host',
		'default'	=> 'localhost',
		'command_arg'	=> 'tty_host',
		'help'		=> <<HELP,
HELP
	},

] ;

sub new {

	my( $class ) = shift ;

	my $self = Stem::Class::parse_args( $attr_spec, @_ ) ;
	return $self unless ref $self ;

	my $aio = Stem::AsyncIO->new(

			'object'	=> $self,
			'read_fh'	=> \*STDIN,
			'write_fh'	=> \*STDOUT,
			'read_method'	=> 'stdin_read',
			'closed_method'	=> 'stdin_closed',
	) ;

	$self->{'aio'} = $aio ;

	my $sock_obj = Stem::Socket->new( 
				'object'	=> $self,
				'host'		=> $self->{'host'},
				'port'		=> $self->{'port'},
				'server'	=> $self->{'server'},
	) ;

	$self->{'sock_obj'} = $sock_obj ;

	return( $self ) ;
}


sub connected {

	my( $self, $connected_sock ) = @_ ;

	my( $type, $sock_buf ) ;


	$self->{'connected'} = 1 ;
	$self->{'sock'} = $connected_sock ;

	$type = $self->{'sock_obj'}->type() ;

	if ( $type eq 'server' ) {

		$self->{'sock_obj'}->stop_listening() ;
	}

	$sock_buf = Stem::AsyncIO->new(

			'object'	=> $self,
			'fh'		=> $connected_sock,
			'read_method'	=> 'socket_read',
			'closed_method'	=> 'socket_closed',
	) ;

	$self->{'sock_buf'} = $sock_buf ;
}

sub socket_read {

	my( $self, $data_ref ) = @_ ;

	$self->{'aio'}->write( $data_ref ) ;
}

sub socket_closed {

	my( $self ) = @_ ;

	$self->{'connected'} = 0 ;

	$self->{'sock_buf'}->shut_down() ;

	if ( $self->{'sock_obj'}->type() eq 'server' ) {

		$self->{'sock_obj'}->start_listening() ;
	}
	else {

		$self->{'sock_obj'}->connect_to() ;
	}
}

sub stdin_read {

	my( $self, $data_ref ) = @_ ;

	unless ( $self->{'connected'} ) {

		print "TTY::Sock not connected\n" ;
		return ;
	}

	$self->{'sock_buf'}->write( $data_ref ) ;
}

sub stdin_closed {

	my( $self ) = @_ ;


#	print "stdin closed\n" ;

	*STDIN->clearerr() ;
}


1 ;
